buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://download.crashlytics.com/maven' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.+'
        classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'crashlytics'

repositories {
    mavenCentral()
    maven { url 'http://download.crashlytics.com/maven' }
}

android {
    compileSdkVersion project.COMPILE_SDK_VERSION as int
    buildToolsVersion project.BUILD_TOOLS_VERSION

    defaultConfig {
        applicationId "org.es.uremote"
        minSdkVersion project.MIN_SDK_VERSION as int
        targetSdkVersion project.TARGET_SDK_VERSION as int
        versionCode 1
        versionName '0.1.4'

        // For each application variants (release, debug, etc.)
        applicationVariants.all { variant ->

            // Create custom apk names
            File file = variant.outputFile
            String date = (new Date()).format('yyyy-MM-dd')
            String filename = date + "_" + project.name + "-v" + variant.versionName + "-" + variant.buildType.name + ".apk"
            variant.outputFile = new File(file.parent, filename)
        }
    }

    signingConfigs {
        debug {
            storeFile rootProject.file('../keystore/debug.keystore')
        }

        release {
            storeFile rootProject.file('../keystore/release.keystore')

            File propertiesFile = rootProject.file('../keystore/keystore.properties')
            Properties properties = new Properties();
            properties.load(new FileInputStream(propertiesFile));

            storePassword properties['storePassword']
            keyAlias properties['keyAlias']
            keyPassword properties['keyPassword']
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"

            jniDebugBuild true
            debuggable true
            signingConfig signingConfigs.debug
        }
        release {
            jniDebugBuild false
            debuggable false
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile project(':volley')
    compile 'com.google.android:support-v4:r7'
    compile 'com.google.protobuf:protobuf-java:2.5.0'
    compile 'com.crashlytics.android:crashlytics:1.+'
}
