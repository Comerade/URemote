buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://download.crashlytics.com/maven' }

    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.9.+'
        classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.+'

    }
}

apply plugin: 'android'
apply plugin: 'crashlytics'

repositories {
    mavenCentral()
    maven { url 'http://download.crashlytics.com/maven' }

}

android {
    compileSdkVersion 19
    buildToolsVersion '19.0.3'

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 19
        versionCode 1
        versionName "0.9"

        applicationVariants.all { variant ->

            def file = variant.outputFile
            def date = (new Date()).format('yyyy-MM-dd')

            variant.outputFile = new File(file.parent,
                    date + "_" + project.name + "-v" + defaultConfig.versionName + "-" + variant.buildType.name + ".apk")
        }
    }

    signingConfigs {
        debug {
            storeFile rootProject.file('../keystore/debug.keystore')
        }

        release {
            storeFile rootProject.file('../keystore/release.keystore')

            def propsFile = rootProject.file('../keystore/keystore.properties')
            def props = new Properties()
            props.load(new FileInputStream(propsFile))

            storePassword props['storePassword']
            keyAlias props['keyAlias']
            keyPassword props['keyPassword']
        }
    }

    buildTypes {
        debug {
            packageNameSuffix ".debug"

            jniDebugBuild true
            debuggable true
            signingConfig signingConfigs.debug
        }
        release {
            jniDebugBuild false
            debuggable false
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile 'com.google.android:support-v4:r7'
    compile 'com.google.protobuf:protobuf-java:2.5.0'
    compile 'com.crashlytics.android:crashlytics:1.+'

}
